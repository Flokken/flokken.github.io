---
title: 字节码案例及详解
date: 2023-07-31
categories: 
  - 后端开发
  - 深入理解JVM
---

## 一个方法的加载

### 原始java代码

~~~java
package cn.itcast.jvm.t3.bytecode;
/**
* 演示 字节码指令 和 操作数栈、常量池的关系
*/
public class Demo3_1 {
	public static void main(String[] args) {
        int a = 10;
        int b = Short.MAX_VALUE + 1;
        int c = a + b;
        System.out.println(c);
	}
}
~~~

### javap反编译字节码

先用javac 编译这个java文件，然后用javap反编译

javap -v Demo3_1.class

### 加载过程

涉及的东西有堆，执行引擎，栈帧，方法区，运行时常量池。

#### 常量池载入运行时常量池

首先是吧常量池中的数据加载到运行时常量池

![image-20230731221021547](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731221021547.png)

#### 方法字节码载入方法区

![image-20230731221355317](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731221355317.png)

#### main 线程开始运行，分配栈帧内存

![image-20230731221553908](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731221553908.png)

#### 执行引擎开始执行字节码

bipush 10

- 将一个 byte 压入操作数栈（其长度会补齐 4 个字节），类似的指令还有 sipush 
- 将一个 short 压入操作数栈（其长度会补齐 4 个字节）
- ldc 将一个 int 压入操作数栈 ldc2_w 将一个 long 压入操作数栈（分两次压入，因为 long 是 8 个字节）
- 这里小的数字都是和字节码指令存在一起，超过 short 范围的数字存入了常量池

![image-20230731221710017](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731221710017.png)

istore 1

将操作数栈顶数据弹出，存入局部变量表的 slot 1

![image-20230731221739943](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731221739943.png)

ldc #3

- 从常量池加载 #3 数据到操作数栈 
- 注意 Short.MAX_VALUE 是 32767，所以 32768 = Short.MAX_VALUE + 1 实际是在编译期间计算 好的

![image-20230731221756623](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731221756623.png)

istore 2

将操作数栈顶数据弹出，存入局部变量表的 slot 2

![image-20230731221923095](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731221923095.png)

iadd

加上1

![image-20230731222139056](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731222139056.png)

istore_3

把栈顶的数存到slot3

![image-20230731222314786](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731222314786.png)

getstatic #4

取#4位置的常量，也就是sout的引用

![image-20230731222335587](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731222335587.png)

iload_3

把slot3的数入栈

![image-20230731222416185](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731222416185.png)

invokevirtual #5

- 找到常量池 #5 项
- 定位到方法区 java/io/PrintStream.println:(I)V 方法 
- 生成新的栈帧（分配 locals、stack等） 
- 传递参数，执行新栈帧中的字节码

![image-20230731222553685](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731222553685.png)

- 执行完毕，弹出栈帧
- 清除 main 操作数栈内容

![image-20230731222617089](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230731222617089.png)

return 

- 完成 main 方法调用，弹出 main 栈帧
-  程序结束

## 练习：分析i++

有java代码

~~~java
package com.inet;
/**
* 从字节码角度分析 a++ 相关题目
*/
public class test {
    public static void main(String[] args) {
        int a = 10;
        int b = a++ + ++a + a--;
        System.out.println(a);
        System.out.println(b);
    }   
}
~~~

先`javac test.java`将其编译为字节码，然后`javap -c -verbose test`将其反编译

`-verbose`可以更加详细的给出分析信息，比如常量池，访问标志等等

~~~shell
public class com.inet.test
  minor version: 0
  major version: 61
  flags: (0x0021) ACC_PUBLIC, ACC_SUPER
  this_class: #19                         // com/inet/test
  super_class: #2                         // java/lang/Object
  interfaces: 0, fields: 0, methods: 2, attributes: 1
Constant pool:
   #1 = Methodref          #2.#3          // java/lang/Object."<init>":()V
   #2 = Class              #4             // java/lang/Object
   #3 = NameAndType        #5:#6          // "<init>":()V
   #4 = Utf8               java/lang/Object
   #5 = Utf8               <init>
   #6 = Utf8               ()V
   #7 = Fieldref           #8.#9          // java/lang/System.out:Ljava/io/PrintStream;
   #8 = Class              #10            // java/lang/System
   #9 = NameAndType        #11:#12        // out:Ljava/io/PrintStream;
  #10 = Utf8               java/lang/System
  #11 = Utf8               out
  #12 = Utf8               Ljava/io/PrintStream;
  #13 = Methodref          #14.#15        // java/io/PrintStream.println:(I)V
  #14 = Class              #16            // java/io/PrintStream
  #15 = NameAndType        #17:#18        // println:(I)V
  #16 = Utf8               java/io/PrintStream
  #17 = Utf8               println
  #18 = Utf8               (I)V
  #19 = Class              #20            // com/inet/test
  #20 = Utf8               com/inet/test
  #21 = Utf8               Code
  #22 = Utf8               LineNumberTable
  #23 = Utf8               main
  #24 = Utf8               ([Ljava/lang/String;)V
  #25 = Utf8               SourceFile
  #26 = Utf8               test.java
{
  public com.inet.test();
    descriptor: ()V
    flags: (0x0001) ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 5: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: (0x0009) ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=1
         0: bipush        10
         2: istore_1
         3: iload_1
         4: iinc          1, 1
         7: iinc          1, 1
        10: iload_1
        11: iadd
        12: iload_1
        13: iinc          1, -1
        16: iadd
        17: istore_2
        18: getstatic     #7                  // Field java/lang/System.out:Ljava/io/PrintStream;
        21: iload_1
        22: invokevirtual #13                 // Method java/io/PrintStream.println:(I)V
        25: getstatic     #7                  // Field java/lang/System.out:Ljava/io/PrintStream;
        28: iload_2
        29: invokevirtual #13                 // Method java/io/PrintStream.println:(I)V
        32: return
      LineNumberTable:
        line 7: 0
        line 8: 3
        line 9: 18
        line 10: 25
        line 11: 32
}
~~~

分析： 

- 注意 iinc 指令是直接在局部变量 slot 上进行运算 
- **a++ 和 ++a 的区别是先执行 iload 还是 先执行 iinc（重）**

![image-20230801164020558](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230801164020558.png)

![image-20230801164148748](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230801164148748.png)

![image-20230801164202736](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230801164202736.png)

![image-20230801164228824](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230801164228824.png)

![image-20230801164239987](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230801164239987.png)

![image-20230801164248776](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230801164248776.png)

## 构造方法

### 	`<cinit>`()v

- `<cinit>` 是类初始化方法的特殊标识符，表示类的初始化方法。
- `()V` 表示该方法没有任何参数，返回类型为 `void`。
- 类初始化方法在类加载时执行，用于初始化类的静态字段和执行其他类级别的初始化操作。
- 类初始化方法只会执行一次，在类加载期间进行，确保类的静态字段在类加载时被正确初始化。
- 是由编译器自动生成的，编译阶段由编译器自动添加到字节码之中

~~~java
public class Demo3_8_1 {
	static int i = 10;
    static {
    	i = 20;
    }
    static {
    	i = 30;
    }
~~~

编译器会按从上至下的顺序**，收集所有 static 静态代码块和静态成员赋值的代码，合并为一个特殊的方法**<cinit>()v:

~~~shell
0: bipush 10
2: putstatic #2 // Field i:I
5: bipush 20
7: putstatic #2 // Field i:I
10: bipush 30
12: putstatic #2 // Field i:I
15: return
~~~

<cinit>()v:在类加载和初始化阶段被调用

~~~java
    public Demo3_8_2(String a, int b) {
        this.a = a;
        this.b = b;
	}
	public static void main(String[] args) {
    	Demo3_8_2 d = new Demo3_8_2("s3", 30);
            System.out.println(d.a);
            System.out.println(d.b);
	}	
}

~~~

### `<init>()v`

- `<init>` 是构造函数的特殊标识符，表示实例对象的初始化方法。
- `()V` 表示该方法没有任何参数，返回类型为 `void`。
- 当创建一个新对象时，会调用其对应类的构造函数（如果有的话），执行对象的初始化工作。字节码中的 `<init>()V` 方法就是对应类的构造函数。
- 实例构造函数用于初始化对象的实例字段、执行其他必要的初始化操作，**以及调用父类构造函数**。
- 是由编译器自动生成的，编译阶段由编译器自动添加到字节码之中

~~~java
public class Demo3_8_2 {
	private String a = "s1";
	{
		b = 20;
	}
    
	private int b = 10;
    
	{
		a = "s2";
	}
	public Demo3_8_2(String a, int b) {
		this.a = a;
		this.b = b;
	}
	public static void main(String[] args) {
		Demo3_8_2 d = new Demo3_8_2("s3", 30);
		System.out.println(d.a);
		System.out.println(d.b);
	}
}
~~~

编译器会按从上至下的顺序，收集所有 {} 代码块和成员变量赋值的代码，**形成新的构造方法，但原始构 造方法内的代码总是在最后**

~~~shell
public cn.itcast.jvm.t3.bytecode.Demo3_8_2(java.lang.String, int);
	descriptor: (Ljava/lang/String;I)V
	flags: ACC_PUBLIC
	Code:
		stack=2, locals=3, args_size=3
            0: aload_0
            1: invokespecial #1 // super.<init>()V
            4: aload_0
            5: ldc #2 // <- "s1"
            7: putfield #3 // -> this.a
            10: aload_0
            11: bipush 20 // <- 20
            13: putfield #4 // -> this.b
            16: aload_0
            17: bipush 10 // <- 10
            19: putfield #4 // -> this.b
            22: aload_0
            23: ldc #5 // <- "s2"
            25: putfield #3 // -> this.a
            28: aload_0 // ------------------------------
            29: aload_1 // <- slot 1(a) "s3" |
            30: putfield #3 // -> this.a |
            33: aload_0 |
            34: iload_2 // <- slot 2(b) 30 |
            35: putfield #4 // -> this.b --------------------
            38: return
        LineNumberTable: ...
        LocalVariableTable:
			Start Length Slot Name Signature
                0 39 0 this Lcn/itcast/jvm/t3/bytecode/Demo3_8_2;
                0 39 1 a Ljava/lang/String;
                0 39 2 b I
	MethodParameters: ...
~~~

## 方法调用

不同的方法调用对应的字节码指令

~~~java
public class Demo3_9 {
	public Demo3_9() { }
	private void test1() { }
	private final void test2() { }
	public void test3() { }
	public static void test4() { }
	public static void main(String[] args) {
        Demo3_9 d = new Demo3_9();
        d.test1();
        d.test2();
        d.test3();
        d.test4();
        Demo3_9.test4();
	}
}
~~~

其对应字节码

~~~shell
0: new #2 // class cn/itcast/jvm/t3/bytecode/Demo3_9
3: dup
4: invokespecial #3 // Method "<init>":()V
7: astore_1
8: aload_1
9: invokespecial #4 // Method test1:()V
12: aload_1
13: invokespecial #5 // Method test2:()V
16: aload_1
17: invokevirtual #6 // Method test3:()V
20: aload_1
21: pop
22: invokestatic #7 // Method test4:()V
25: invokestatic #7 // Method test4:()V
28: return
~~~

- `new` 是创建对象，给对象分配堆内存，执行成功会将对象引用压入操作数栈。

- `dup` 是赋值操作数栈栈顶的内容，本例即为对象引用，为什么需要两份引用呢，一个是要配合 `invokespecial` 调用该对象的构造方法 `<init>:()V` （会消耗掉栈顶一个引用），另一个要配合 `astore_1` 赋值给局部变量。
- 最终方法（final），私有方法（private），构造方法都是由 `invokespecial` 指令来调用，属于静态绑定。

- 普通成员方法是由 `invokevirtual` 调用，**属于动态绑定，即支持多态。**
- 成员方法与静态方法调用的另一个区别是，**执行方法前是否需要对象引用。**
- 比较有意思的是 `d.test4();` 是通过对象引用调用一个静态方法，可以看到在调用 `invokestatic` 之前执行了 `pop` 指令，把对象引用从操作数栈弹掉了。

- 还有一个执行 `invokespecial` 的情况是通过 `super` 调用父类方法。

## 多态的原理

> 提一嘴应用
>
> 继承中的子类重写父类的方法、
>
> 同一个类中方法的重载（名字相同，参数列表不相同）
>
> 接口的多态性：同一个接口可以被不同实现类来实现
>
> 抽象类多态性：同一个抽象类也可以被不同类实现他的抽象方法

 `invokevirtual` 指令允许在运行时**根据实际对象类型动态绑定方法调用**。

> 在 Java 中，当使用父类引用指向子类对象时，编译器会根据引用的声明类型来确定使用哪个方法。但是，在运行时，实际调用的是对象的方法，而不是引用的方法。这就是多态性的体现。

通过一个案例来分析

> 参考黑马的jvm教程，03类加载技术与字节码
>
> 不去详细过程，只是了解多态在编译时是个什么情况

~~~java
package cn.itcast.jvm.t3.bytecode;

import java.io.IOException;

/**
 * 演示多态原理，注意加上下面的 JVM 参数，禁用指针压缩
 * -XX:-UseCompressedOops -XX:-UseCompressedClassPointers
 */
public class Demo3_10 {
    public static void test(Animal animal) {
        animal.eat();
        System.out.println(animal.toString());
    }

    public static void main(String[] args) throws IOException {
        test(new Cat());
        test(new Dog());
        System.in.read();
    }
}
abstract class Animal {
    public abstract void eat();

    @Override
    public String toString() {
        return "我是" + this.getClass().getSimpleName();
    }
}
class Dog extends Animal {
    @Override
    public void eat() {
        System.out.println("啃骨头");
    }
}
class Cat extends Animal {
    @Override
    public void eat() {
        System.out.println("吃鱼");
    }
}
~~~

概括：**当执行 invokevirtual 指令时，**

- 先通过栈帧中的对象引用找到对象
- 分析对象头，找到对象的实际 Class
- Class 结构中有 vtable，它在类加载的链接阶段就已经根据方法的重写规则生成好了
- 查表得到方法的具体地址
- 执行方法的字节码

这里来看Dog对象，

![image-20230802085758835](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230802085758835.png)、

查看内存结构

点击超链接可以看到对象的内存结构，此对象没有任何属性，因此只有对象头的 16 字节，前 8 字节是 MarkWord，后 8 字节就是对象的 Class 指针

![image-20230802085854916](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230802085854916.png)

查看对象 Class 的内存地址

![image-20230802085909934](https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230802085909934.png)

查看类的 vtable

<img src="https://typora-1309665611.cos.ap-nanjing.myqcloud.com/typora/image-20230802090001126.png"/>

可以找到 **Dog Class 的 vtable 长度为 6，意思就是 Dog 类有 6 个虚方法（多态 相关的，final，static 不会列入）**

~~~shell
mem 0x000000001b7d41e0 6
0x000000001b7d41e0: 0x000000001b3d1b10
0x000000001b7d41e8: 0x000000001b3d15e8
0x000000001b7d41f0: 0x000000001b7d35e8
0x000000001b7d41f8: 0x000000001b3d1540
0x000000001b7d4200: 0x000000001b3d1678
0x000000001b7d4208: 0x000000001b7d3fa8
~~~

**这就是其6个方法的入口地址**

通过 Tools -> Class Browser 查看每个类的方法定义，比较可知

~~~java
Dog - public void eat() @0x000000001b7d3fa8
Animal - public java.lang.String toString() @0x000000001b7d35e8;
Object - protected void finalize() @0x000000001b3d1b10;
Object - public boolean equals(java.lang.Object) @0x000000001b3d15e8;
Object - public native int hashCode() @0x000000001b3d1540;
Object - protected native java.lang.Object clone() @0x000000001b3d1678;
~~~

对号入座，

- 发现 eat() 方法是 Dog 类自己的 
- toString() 方法是继承 String 类的
-  finalize() ，equals()，hashCode()，clone() 都是继承 Object 类的

## try-finally

> 分析其字节码，待补充

##  synchronized

> 分析其字节码，待补充
